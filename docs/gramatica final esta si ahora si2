<Inicial> ::= <ListaClasesEInterfaces> eof

<ListaClasesEInterfaces> ::= <ModificadorOpcional> <ListaClasesEInterfaces2> | 
<ListaClasesEInterfaces2> ::= <Clase> <ListaClasesEInterfaces> | <Interfaz> <ListaClasesEInterfaces>  

<Clase> ::= class idClase <GenericidadOpcional> <HerenciaOpcional> { <ListaMiembros> }
<Interfaz> ::= interface idClase <GenericidadOpcional> <ExtendsOpcional> { <ListaMiembrosInterfaz> }

<ModificadorOpcional> ::= abstract | static | final | 
<Modificador> ::= abstract | static | final
<VisibilidadOpcional> ::= public | private | 

<HerenciaOpcional> ::= extends idClase <GenericidadOpcional>| implements idClase <GenericidadOpcional>| 
<ExtendsOpcional> ::= extends idClase <GenericidadOpcional>| 

<GenericidadODiamanteOpcional> ::= < <IdClaseOpcional> > | 
<IdClaseOpcional> ::= idClase | 

<GenericidadOpcional> ::= <  idClase  > | 

<ListaMiembros> ::= <VisibilidadOpcional> <Miembro> <ListaMiembros> | 
<ListaMiembrosInterfaz> ::= <VisibilidadOpcional> <MiembroInterfaz> <ListaMiembrosInterfaz> | 

<Miembro> ::= <Tipo> idMetVar <AtributoMetodo> | <Modificador> <TipoMetodo> idMetVar <ArgsFormalesYBloqueOpcional> | void idMetVar <ArgsFormalesYBloqueOpcional> | <Constructor>
<MiembroInterfaz> ::= <Tipo> idMetVar = <Expresion> ; | <Modificador> <TipoMetodo> idMetVar <ArgsFormalesYBloqueOpcional>  | void idMetVar <ArgsFormalesYBloqueOpcional>

<AtributoMetodo> ::= <AsignacionOpcional> ; | <ArgsFormales> <BloqueOpcional>
<ArgsFormalesYBloqueOpcional> ::= <ArgsFormales> <BloqueOpcional>

<Atributo> ::= <Tipo> idMetVar <AsignacionOpcional> ;										### ahora se puede inicializar el atributo
<AsignacionOpcional> ::=   =   <Expresion> | 
<AtributoInicializado> ::= <Tipo> idMetVar = <Expresion> ;

<Metodo> ::= <ModificadorOpcional> <TipoMetodo> idMetVar <ArgsFormales> <BloqueOpcional>

<Constructor> ::= public idClase <ArgsFormales> <Bloque>

<TipoMetodo> ::= <Tipo> | void

<Tipo> ::= <TipoPrimitivo> | idClase <GenericidadOpcional>

<TipoPrimitivo> ::= boolean | char | int

<ArgsFormales> ::= ( <ListaArgsFormalesOpcional> )


<ListaArgsFormalesOpcional> ::= <ListaArgsFormales> | 

<ListaArgsFormales> ::= <ArgFormal> <ListaArgsFormales2>
<ListaArgsFormales2> ::= , <ArgFormal> <ListaArgsFormales2> | 									#####################3

<ArgFormal> ::= <Tipo> idMetVar

<BloqueOpcional> ::= <Bloque> | ;

<Bloque> ::= { <ListaSentencias> }

<ListaSentencias> ::= <Sentencia> <ListaSentencias> | 

<Sentencia> ::= ;
<Sentencia> ::= <AsignacionOLlamada> ;
<Sentencia> ::= <VarLocal> ;																		###VarLocal y AsignacionOLlamada tienen conflicto ya que ambos tinene idClase en
<Sentencia> ::= <Return> ;																			###primeros, varLocal en <Tipo> y AsignacionOLlamada en expresion -> LlamadaMetodoEstatico
<Sentencia> ::= <If>
<Sentencia> ::= <While>
<Sentencia> ::= <Bloque>
<Sentencia> ::= <For>

<For> ::= for ( <Expresion> <CondicionFor> ) | for( <VarLocal> <CondicionFor>)						### EXPRESION Y varLocal tienen idClase en primeros
<CondicionFor> ::= ; <ExpresionCompuesta> ; <Expresion> | : <Expresion>

<AsignacionOLlamada> ::= <Expresion>

<VarLocal> ::= var idMetVar = <ExpresionCompuesta>  | <Tipo> idMetVar <DeclaracionMultiple> <AsignacionOpcional> 
<DeclaracionMultiple> ::= , idMetVar <DeclaracionMultiple> | 

<Return> ::= return <ExpresionOpcional>

<ExpresionOpcional> ::= <Expresion> | 

<If> ::= if ( <Expresion> ) <Sentencia> <SentenciaElse>
<SentenciaElse> ::= else <Sentencia> | 

<While> ::= while ( <Expresion> ) <Sentencia>

<Expresion> ::= <ExpresionCompuesta> <Expresion2>
<Expresion2> ::= <OperadorAsignacion> <ExpresionCompuesta> <OperadorTernarioOpcional> | <OperadorTernarioOpcional>

<OperadorTernarioOpcional> ::=  ? <Expresion> : <ExpresionCompuesta> <OperadorTernarioOpcional> | 

<OperadorAsignacion> ::= = | += | -=

<ExpresionCompuesta> ::= <ExpresionBasica> <OperadorBinario> <ExpresionBasica> <ExpresionCompuesta2>
<ExpresionCompuesta2> ::= <OperadorBinario> <ExpresionBasica> <ExpresionCompuesta2> | 


<OperadorBinario> ::= || | && | == | != | < | > | <= | >= | + | - | * | / | %

<ExpresionBasica> ::= <OperadorUnario> <Operando>
<ExpresionBasica> ::= <Operando>

<OperadorUnario> ::= + | ++ | âˆ’ | âˆ’âˆ’ | !

<Operando> ::= <Primitivo>
<Operando> ::= <Referencia>

<Primitivo> ::= true | false | intLiteral | charLiteral | null

<Referencia> ::= <Primario> <Referencia2>
<Referencia2> ::= <VarMetodoEncadenado> <Referencia2> | 													##############################

<Primario> ::= this
<Primario> ::= stringLiteral
<Primario> ::= <AccesoVarLlamadaMetodo>
<Primario> ::= <LlamadaConstructor>
<Primario> ::= <LlamadaMetodoEstatico>
<Primario> ::= <ExpresionParentizada>

<AccesoVarLlamadaMetodo> ::= idMetVar <ArgsActualesOpcional>

<ArgsActualesOpcional> ::= <ArgsActuales> | 

<LlamadaConstructor> ::= new idClase <GenericidadODiamanteOpcional> <ArgsActuales>

<ExpresionParentizada> ::= ( <Expresion> )

<LlamadaMetodoEstatico> ::= idClase . idMetVar <ArgsActuales>

<ArgsActuales> ::= ( <ListaExpsOpcional> )

<ListaExpsOpcional> ::= <ListaExps> | 

<ListaExps> ::= <Expresion> <ListaExps2>
<ListaExps2> ::= , <Expresion> <ListaExps2> | 

<VarMetodoEncadenado> ::= . idMetVar <ArgsActualesOpcional>